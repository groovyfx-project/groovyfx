apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

// ---------------- dependencies ------------------

buildscript {
    final javafxHome = System.env['JAVAFX_HOME']
    if (javafxHome) {
      ext.javafxJar = "${javafxHome}/rt/lib/jfxrt.jar"
    } else {
      final javaHome = System.env['JAVA_HOME']
      if (javaHome)
        ext.javafxJar = "${javaHome}/jre/lib/jfxrt.jar"
    }
    println "JavaFX runtime jar: ${ext.javafxJar}"
    dependencies {
        classpath files(ext.javafxJar)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    groovy       "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile      'asm:asm:3.3.1'
    compile      files(ext.javafxJar)
    testCompile  'junit:junit:4.10'
}

task wrap(type:Wrapper, description:"create a gradlew") {
    gradleVersion = '1.0-rc-3'
}

apply {
    from 'buildSrc/scripts/demo.gradle'
    from 'buildSrc/scripts/mavenCentralDeploy.gradle'
    from 'buildSrc/scripts/docsDependencies.gradle'
    from 'buildSrc/scripts/docs.gradle'
}

signArchives.onlyIf {
    project.hasProperty('signing.keyId')
}

uploadArchives.onlyIf {
    project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')
}

// ---------------- check environment ------------------

if(!ext.javafxJar) {
    println """Please set environment variable JAVAFX_HOME
               to the directory that contains /rt/lib/jfxrt.jar
               in version $requiredJavaFxVersion""".stripIndent()
    System.exit 1
}

ext.actualJavaFXVersion = com.sun.javafx.runtime.VersionInfo.runtimeVersion
if (! ext.actualJavaFXVersion.startsWith(requiredJavaFxVersion)) {
    println "Required JavaFX version is '$requiredJavaFxVersion' but actual version is '$ext.actualJavaFXVersion'"
    System.exit(1)
}

